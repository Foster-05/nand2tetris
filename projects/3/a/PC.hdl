// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16],inc, load, reset;
    OUT out[16];
    
    PARTS:
    //Create an increment and choose between the previous value or the incremented value
    Inc16(in=latched, out=incremented);
    Mux16(a=latched, b=incremented, sel=inc, out=incout);
    //Choose between the previous value, incremented value, or loaded value
    Mux16(a=incout, b=in, sel=load, out=loadout);
    //Choose between the previous value, incremented value, loaded value, or reset value
    Mux16(a=loadout, b=false, sel=reset, out=repeat);
        
    //Feed output into a register, feed the output back in to the top and to the output
    Register(in=repeat, load=true, out=latched, out=out);    
}
