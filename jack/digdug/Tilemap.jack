/*Tilemap storing game tiles, allowing for the 
manipulation of game tiles */
class Tilemap {
    field int mapWidth;
    field int mapHeight;
    field Array map;

    constructor Tilemap new(int width, int height) {
        let mapWidth = width;
        let mapHeight = height;
        do initializeMap();

        return this;
    }

    method int getTile(int x, int y) {
        var Array arr;
        let arr = map[y];
        return arr[x];
    }

    method void setTile(int x, int y, int value) {
        var Array arr;
        let arr = map[y];
        let arr[x] = value;
        return;
    }

    /*
    initializeMap helper, filling an array of length length with
    arrays of length size.

    @param:
    arr => array to be filled
    length => length of arr
    size => size of arrays to fill with
    */
    method void fillArray(Array arr, int length, int size) {
        var int index;
        let index = 0;

        while (index < length) {
            let arr[index] = Array.new(size);
            let index = index + 1;
        }

        return;
    }

    /*
    initializeMap helper, filling an array of length length with
    an integer value

    @param:
    arr => array to be filled
    length => length of arr
    value => integer value to fill with
    */
    method void fillInt(Array arr, int length, int value) {
        var int index;
        let index = 0;

        while (index < length) {
            let arr[index] = value;
            let index = index + 1;
        }

        return;

    }

    /*
    initializes map to a generic sample map
    */
    method void initializeMap() {

        var int index;
        var Array arr;

        let map = Array.new(16);

        do fillArray(map, mapHeight, mapWidth);

        let index = 2;

        //Creates air on top two layers
        do fillInt(map[0], mapWidth, 0);
        do fillInt(map[1], mapWidth, 0);

        //Fills rest of map with dirt
        while (index < mapHeight) {
            do fillInt(map[index], mapWidth, 1);
            let index = index + 1;
        }

        //Adds a few tunnels of air within the dirt
        let arr = map[3];
        let arr[3] = 0;
        let arr[4] = 0;
        let arr[5] = 0;
        let arr[6] = 0;

        let arr = map[4];
        let arr[3] = 0;
        let arr[4] = 0;
        let arr[5] = 0;
        let arr[6] = 0;
        
        return;
    }
}
	
